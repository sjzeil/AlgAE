plugins {
    id 'java'
	id 'project-report'
	id 'pmd'
	id 'jacoco'
	id 'ivy-publish'
}

java {
	sourceCompatibility = JavaVersion.toVersion(11)
	targetCompatibility = JavaVersion.toVersion(11)
}


dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'javax.help:javahelp:2.0.05'
	testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}


test {
	ignoreFailures = true
    useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	
}



// Reporting


pmd {
    ignoreFailures = true
    consoleOutput = false
}

pmdTest.enabled = false
check.dependsOn htmlDependencyReport

task reports (dependsOn: ['htmlDependencyReport', 'javadoc', 'check']) {
    description 'Generate all reports for this project'
}

javadoc.failOnError=false


// Publishing

// Credentials are loaded from ~/.gradle/gradle.properties
if(project.hasProperty("ivyRepoUser")){
    ext.ivyRepoUser = "$ivyRepoUser";
} else {
    ext.ivyRepoUser = "user";
}
if(project.hasProperty("ivyRepoPass")){
    ext.ivyRepoPass = "$ivyRepoPass";
} else {
    ext.ivyRepoPass = "password";
}

publishing {
    publications {
        ivyJava(IvyPublication) {
            organisation 'edu.odu.cs.algae'
            module project.name
            revision project.version
            descriptor.status = 'integration'   // milestone, release
            descriptor.branch = 'v4'

            from components.java
        }
    }

    repositories {
        ivy {
            name 'ivyRepo'
            url 'sftp://linux.cs.odu.edu:22/home/zeil/secure_html/ivyrepo'
            // Readable via https://www.cs.odu.edu/~zeil/ivyrepo
            credentials {
                username project.ivyRepoUser
                password project.ivyRepoPass
            }
        }
    }
}

