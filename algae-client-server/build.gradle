plugins {
    id 'java'
	id 'project-report'
	id 'pmd'
	id 'jacoco'
	id 'maven-publish'
}

java {
	sourceCompatibility = JavaVersion.toVersion(17)
	targetCompatibility = JavaVersion.toVersion(17)
}


dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'javax.help:javahelp:2.0.05'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}


test {
	ignoreFailures = true
    useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	
}



// Reporting


pmd {
    ignoreFailures = true
    consoleOutput = false
}

pmdTest.enabled = false
check.dependsOn htmlDependencyReport

task reports (dependsOn: ['htmlDependencyReport', 'javadoc', 'check']) {
    description 'Generate all reports for this project'
}

javadoc.failOnError=false


// Publishing

publishing {
    publications {
        codegraderLibrary(MavenPublication) {
            from components.java
            groupId = 'edu.odu.cs.algae'
            artifactId = project.name
            version = project.version
            pom {
                name = "algae-client-server"
                description = 'AlgAE Algorithm Animation Engine'
                url = 'https://github.com/sjzeil/AlgAE'
                licenses {
                    license {
                        name = "Educational Community License"
                        url = "https://github.com/sjzeil/AlgAE/blob/master/LICENSE.md"
                    }
                }
                developers {
                    developer {
                        id = 'sjzeil'
                        name = 'Steven Zeil'
                        email = 'szeil@odu.edu'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'Zeil-maven-repo'
            url = '../../mvnrepo'
        }
    }
}

